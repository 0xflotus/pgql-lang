module types

language pgql-lang

test Vertex/edge where boolean expected [[
SELECT n
 MATCH (n) -[e]-> (m)
 WHERE NOT [[n]]
   AND NOT [[e]]
   AND (true AND [[n]])
   AND (true AND [[e]])
   AND (true OR [[n]])
   AND (true OR [[e]])
]] error like "Boolean expected here" at #1, #2, #3, #4, #5, #6

test Vertex/edge where numeric expected [[
SELECT n
 MATCH (n) -[e]-> (m)
 WHERE -[[n]] > 42
   AND -[[e]] > 42
   AND 1 - [[n]] > 42
   AND 1 - [[e]] > 42
   AND 1 + [[n]] > 42
   AND 1 + [[e]] > 42
   AND 1 * [[n]] > 42
   AND 1 * [[e]] > 42
   AND 1 / [[n]] > 42
   AND 1 / [[e]] > 42
   AND 1 % [[n]] > 42
   AND 1 % [[e]] > 42
]] error like "Numeric expected here" at #1, #2, #3, #4, #5, #6, #7, #8, #9, #10, #11, #12

test Illegal vertex/edge comparison [[
SELECT n
 MATCH (n) -[e]-> (m)
 WHERE [[n]] < m
   AND [[e]] < 42
   AND [[n]] > m
   AND [[e]] > 42
   AND [[n]] <= m
   AND [[e]] <= 42
   AND [[n]] >= m
   AND [[e]] >= 42
]] error like "Comparison not allowed because no order is defined for vertices and edges" at #1, #2, #3, #4, #5, #6, #7, #8

test Illegal vertex/edge aggregation [[
SELECT MIN([[n]]) AS min1, MIN([[e]]) AS min2
     , MAX([[n]]) AS max1, MAX([[e]]) AS max1
     , AVG([[n]]) AS avg1, AVG([[e]]) AS avg1
     , SUM([[n]]) AS sum1, SUM([[e]]) AS sum1
 MATCH (n) -[e]-> (m)

]] error like "Aggregate does not allow vertex or edge input" at #1, #2, #3, #4, #5, #6, #7, #8

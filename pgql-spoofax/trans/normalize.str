module normalize

imports

  stratego/-
  signatures/-
  normalized-signatures
  common
  pp

rules

  normalize-before:
    ast -> result
    with
      result := <
      	alltd(norm-UMin);
        innermost(
          norm-NOT +
          norm-singleVertex +
          norm-VertexWithoutBrackets +
          norm-edgeContents
        );
        alltd(norm-string);
        alltd(norm-has-prop);
        alltd(name-unnamed-elems);
        alltd(norm-inlined-references);
        bottomup(try(
           norm-matchElems
        ));
        bottomup(try(origin-track-forced(norm-query)))
      > ast

rules

  norm-UMin = ?UMin(Decimal(string)); !Decimal(<conc-strings> ("-", string))

  norm-NOT = ?Not1(<id>); !Not(<id>)
  norm-NOT = ?Not2(<id>); !Not(<id>)

  norm-string = String(norm-string-helper) + PropRef(id, norm-string-helper)
  norm-string-helper = (un-double-quote + un-single-quote)
                     ; string-replace(|"\\\"", "\"")
                     ; string-replace(|"\\'", "'")
                     ; string-replace(|"\\\\", "\\")

rules

  norm-singleVertex = ?SingleVertex(elemContents); !Path(Vertex(elemContents), [], None()) // SELECT * WHERE ()

  norm-VertexWithoutBrackets = ?VertexWithoutBrackets(v); !Vertex(ElemContents(Some(<origin-track-forced(<conc-strings> (v, <VERTEX_WITHOUT_BRACKETS>))>), None(), None(), None()))

  norm-edgeContents = ?Some(EdgeContents(<id>))

  name-unnamed-elems     = (?PathPattern(_, _) + ?GraphPattern(_)); where (c := <new-counter>); alltd(name-unnamed-elems(|c))
  name-unnamed-elems(|c) = ?ElemContents(None(), type, id', constraints); !ElemContents(Some(<unique-name(|c)>), type, id', constraints)
  name-unnamed-elems(|c) = OutConn(?None(); name-unnamed-elems-helper(|c)) +
                           InConn(?None(); name-unnamed-elems-helper(|c))
  name-unnamed-elems(|c) = ?OutEdge(); !OutConn(<name-unnamed-elems-helper(|c)>) +
                           ?InEdge(); !InConn(<name-unnamed-elems-helper(|c)>) +
                           ?Path(None(), pathPatternRef, kleeneStar); !Path(Some(<unique-name(|c)> ""), pathPatternRef, kleeneStar)
  name-unnamed-elems-helper(|c) = !ElemContents(Some(<unique-name(|c)> ""), None(), None(), None())

  norm-inlined-references = ?ElemContents(Some(varName), _, _, _); alltd(norm-inlined-reference(|varName))
  norm-inlined-reference(|varName) =
    ?Obj(dot); !VarRef(varName) + (?Obj(VarRef(v), dot) + ?PropRef(VarRef(v), _));
    if <eq> (v, varName)
    then !ReferenceInInlinedConstraint(v)
    else !CrossReferenceInInlinedConstraint()
    end +
    (?SelfPropRef(propName) + ?SelfPropRef(dot, propName)); !PropRef(VarRef(varName), propName)

rules

  norm-matchElems:
    GraphPattern(elems) -> GraphPattern(vertices, edges, paths'', constraints)
    with
      paths := <filter(?Path(_, _, _))> elems;
      inlined-constraints-for-vertices := <collect-om(?Vertex(<id>)); map(get-inlined-constraints(|"vertex")); concat> paths;
      inlined-constraints-for-edges := <collect-om(?OutConn(<id>) + ?InConn(<id>)); filter(get-inlined-constraints(|"edge")); concat> paths;
      non-inlined-constraints := <filter(?Constraint(<id>))> elems;
      constraints := <conc; flatten-list; !Constraints(<id>)> (inlined-constraints-for-vertices, inlined-constraints-for-edges, non-inlined-constraints);
      paths' := <alltd(?ElemContents(Some(<id>), _, _, _))> paths;
      vertices := <collect-om(?Vertex(_)); !Vertices(<id>)> paths';
      connections := <get-edges> paths';
      edges := <filter(?Edge(_, _, _, _)); !Edges(<id>)> connections;
      paths'' := <filter(?Path(_, _, _, _, _)); !Paths(<id>)> connections

  norm-path-pattern:
    PathPattern(name, path) -> PathPattern(name, vertices, connections, constraints)
    with
      inlined-constraints-for-vertices := <collect-om(?Vertex(<id>)); map(get-inlined-constraints(|"vertex")); concat> path;
      inlined-constraints-for-edges := <collect-om(?OutConn(<id>) + ?InConn(<id>)); filter(get-inlined-constraints(|"edge")); concat> path;
      constraints := <conc; flatten-list; !Constraints(<id>)> (inlined-constraints-for-vertices, inlined-constraints-for-edges);
      path' := <alltd(?ElemContents(Some(<id>), _, _, _))> path;
      vertices := <collect-om(?Vertex(_), conc); !Vertices(<id>)> path';
      connections := <get-edges> [path']

  get-inlined-constraints(|vertexOrEdge):
    ElemContents(Some(var), label, id', otherConstraint) -> [labels, identifiers, otherConstraint']
    with
      labels := <?Some(Labels(<id>)); to-label-constraints(|var, vertexOrEdge) <+ ![]> label;
    	identifiers := <?Some(Ids(<id>)); to-id-constraints(|var) <+ ![]> id';
    	otherConstraint' := <?Some(Constraint(<id>)) <+ ![]> otherConstraint

  to-label-constraints(|var, vertexOrEdge):
    labels -> result
    with
      if "vertex" := vertexOrEdge
      then result := <to-hasLabel-expressions(|var)> labels
      else result := <to-label-equals-expressions(|var)> labels
      end
  to-hasLabel-expressions(|var) = bottomup(try(is-literal; origin-track-forced(!HasLabel(<origin-track-forced(!VarRef(var))> <id>, <id>))))
  to-label-equals-expressions(|var) = bottomup(try(is-literal; origin-track-forced(!Eq(<origin-track-forced(!Label(VarRef(var)))> <id>, <id>))))
  to-id-constraints(|var) = bottomup(try(is-literal; origin-track-forced(!Eq(Id(VarRef(var)), <id>))))

  // base case: no paths
  get-edges:
    [] -> []

  // head is path with only one vertex => get neighbors for the other paths
  get-edges:
    [Path(_, [], _)|otherPaths] -> <get-edges> otherPaths

  // paths with more than one vertex
  get-edges:
    [Path(
      Vertex(n1),
      edgeVertices@[EdgeVertex(e, Vertex(n2))|_],
      _)
    |otherPaths] -> <conc> ([edge], edgesPathTail, edgesTailPaths)
    with
      edge := <to-edge> (n1, e, n2);
      edgesPathTail := <get-edges> edgeVertices;
      edgesTailPaths := <get-edges> otherPaths

  // base case: only one edgeVertex
  get-edges:
    [EdgeVertex(_, _)] -> []
    
  // two or more edgeVertices
  get-edges:
    [EdgeVertex(_, Vertex(n1)) | edgeVertices@[EdgeVertex(e, Vertex(n2))|_]]
    -> <conc> ([edge], edgesPathTail)
    with
      edge := <to-edge> (n1, e, n2);
      edgesPathTail := <get-edges> edgeVertices

  to-edge:
    (n1, e, n2) -> expansion
    with
      switch !e
        case ?OutConn(edgeOrPath):
          src := n1;
          dst := n2;
          dir := Outgoing()
        case ?InConn(edgeOrPath):
          src := n2;
          dst := n1;
          dir := Incoming()
        otherwise: fail
      end;
      switch !edgeOrPath
        case is-string:
          expansion := Edge(src, edgeOrPath, dst, dir)
        case ?Some(Path(Some(pathName), pathPatternName, kleeneStar)):
          expansion := Path(src, dst, pathPatternName, kleeneStar, pathName)
        otherwise: fail
      end

rules

  norm-has-prop = ?Has(varRef, [prop]); !Has(varRef, prop) <+
                  ?Has(varRef, <id>); map(!Has(varRef, <id>)); create-conjunct

  create-conjunct = reverse; !(<Tl>, <Hd>); foldl(create-OR)
  create-OR = ?(exp1, exp2); !And(exp1, exp2)

rules

  norm-query:
    Query(pathPatterns, SelectClause(projection), fromClause, graphPattern, groupByClause, orderByClause, limitOffsets) ->
        Query(pathPatterns', projection', fromClause, graphPattern, groupByClause', orderByElems, limitOffsets')
    with
      pathPatterns' := <map(norm-path-pattern); !PathPatterns(<id>)> pathPatterns;

      expAsGroupVars := <?Some(GroupByClause(<id>)) <+ ![]> groupByClause;
      expAsGroupVars' := <map(norm-ExpAsVar)> expAsGroupVars;
      groupByClause' := <?Some(_); !Some(GroupByClause(expAsGroupVars')) <+ !None()> groupByClause;

      expAsSelectVars := <?ExpAsVars(<id>); map(norm-ExpAsVar); map(replace-terms-with-varRef(|expAsGroupVars')) <+
                          norm-select-star(|graphPattern, expAsGroupVars'); map(norm-ExpAsVar)> projection;
      projection' := Vars(ExpAsVars(expAsSelectVars));

      allExpAsVars := <conc> (expAsGroupVars', expAsSelectVars);
      orderByElems := <try(norm-orderBy(|allExpAsVars))> orderByClause;

      limitOffsets' := <norm-limitOffsets> limitOffsets

  norm-select-star(|graphPattern, group-exps):
    Star() -> expAsSelectVars
    with if [] := group-exps
           then vars := <collect((?Vertex(<id>) + ?Edge(_, <id>, _, _)); not(is-substring(GENERATED)))> graphPattern
           else vars := <map(?ExpAsVar(_, <id>))> group-exps
         end
       ; expAsSelectVars := <map(!ExpAsVar(VarRef(<id>), None()))> vars

  norm-ExpAsVar = ?ExpAsVar(exp, Some(VarAssign(VarDef(v)))); !ExpAsVar(exp, v)
  norm-ExpAsVar = ?ExpAsVar(exp, None()); !AnonymousExpAsVar(exp, <?VarRef(<id>) <+ origin-track-forced(origin-text)> exp)

  replace-terms-with-varRef(|expressions):
    exp -> exp'
    with exp' := <foldl(replace-terms-with-varRef)> (expressions, exp)

  replace-terms-with-varRef = (?(ExpAsVar(term, var), <id>) + ?(AnonymousExpAsVar(term, var), <id>)); alltd(
    is-aggregate; ?term; !VarRef(var) + // for ORDER BY
    is-aggregate + ?term; !VarRef(var)) // for SELECT: never look into aggregates

  norm-orderBy(|allExpAsVars):
    Some(OrderByClause(elems)) -> Some(OrderByClause(elems'))
    with
      elems' := <map(norm-orderBy-ordering); map(norm-order-expression(|allExpAsVars))> elems

  norm-orderBy-ordering =
    ((?Desc1(<id>) + ?Desc2(<id>)); !OrderByElem(<id>, Desc())) <+
    ((try(?Asc1(<id>) + ?Asc2(<id>))); !OrderByElem(<id>, Asc()))

  norm-order-expression(|group-and-select-expressions):
    OrderByElem(exp, ordering) -> OrderByElem(exp', ordering)
    with
      exp' := <replace-terms-with-varRef(|group-and-select-expressions)> exp

  norm-limitOffsets = ?None(); !LimitOffsetClauses(None(), None())
  norm-limitOffsets = ?Some(LimitOffsetClauses(limitClause, offsetClause)); !LimitOffsetClauses(Some(limitClause), offsetClause)
  norm-limitOffsets = ?Some(OffsetLimitClauses(offsetClause, limitClause)); !LimitOffsetClauses(limitClause, Some(offsetClause))

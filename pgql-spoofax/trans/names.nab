module names

imports

  signatures/-

namespaces

  PathPattern // regular PATH pattern
  WhereVar    // var defined in WHERE
  GroupVar    // var defined in GROUP BY
  SelectVar   // var defined in SELECT
  Var         // var defined in WHERE/GROUP BY/SELECT

binding rules

  Query(_, _, _, _, _, _, _):
    scopes WhereVar, SelectVar, GroupVar, PathPattern

  PathPattern(p, _, _, _):
    defines unique PathPattern p
    scopes WhereVar, SelectVar, GroupVar

  Vertex(n):
    defines non-unique Var n
    defines non-unique WhereVar n of type VertexTy()

  Edge(_, e, _, _):
    defines non-unique Var e
    defines unique WhereVar e of type EdgeTy()

  Path(_, _, _, _, p):
    defines non-unique Var p
    defines unique WhereVar p of type PathTy()

  PathPatternRef(p):
    refers to PathPattern p

  ExpAsVar(exp, v): // in SELECT when there is no GROUP BY
    defines unique SelectVar v of type ty where exp has type ty // unlike SQL but like SPARQL, we don't allow duplicate column names
  AnonymousExpAsVar(exp, v):
    defines unique SelectVar v of type ty where exp has type ty

  ExpAsGroupVar(exp, v): // in GROUP BY
    defines unique GroupVar v of type ty where exp has type ty // unlike SQL but like SPARQL, we don't allow duplicate group keys
    defines unique Var v
  AnonymousExpAsGroupVar(exp, v):
    defines unique GroupVar v of type ty where exp has type ty

  VarRef(v):
    refers to WhereVar v // refers to a variable in the WHERE clause

  GroupRef(v):
    refers to GroupVar v // refers to a variable in the GROUP BY clause

  SelectOrGroupRef(v): // in ORDER BY while there is a GROUP BY
    refers to GroupVar v // e.g. in [[SELECT * WHERE (n1) GROUP BY n1 AS n2 ORDER BY n2]], 'n2' in ORDER BY refers to 'n2' in GROUP BY
      otherwise refers to SelectVar v // e.g. in [[SELECT AVG(n.age) as avgAge WHERE (n) ORDER BY avgAge]], 'avgAge' in ORDER BY refers to 'avgAge' in SELECT

  VarOrSelectRef(v): // in ORDER BY while there is no GROUP BY
    refers to WhereVar v // e.g. in [[SELECT n.name WHERE (n) ORDER BY n.age], 'n' in ORDER BY refers to 'n' in WHERE
      otherwise refers to SelectVar v // e.g. in [[SELECT n.name AS nName WHERE (n) ORDER BY nName], 'nName' in ORDER BY refers to 'nName' in SELECT

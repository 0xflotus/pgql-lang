module names

imports
  
  signatures/-
  
namespaces
  
  Var       // var defined in WHERE
  SelectVar // var defined in SELECT
  GroupVar  // var defined in GROUP BY
  PathPattern
  
binding rules

  Query(_, _, _, _, _, _, _):
    scopes Var, SelectVar, GroupVar, PathPattern

  PathPattern(p, _, _, _):
    defines unique PathPattern p
    scopes Var, SelectVar, GroupVar

  Node(n):
    defines non-unique Var n of type NodeTy()

  Edge(_, e, _, _):
    defines unique Var e of type EdgeTy()

  Path(_, _, _, _, p):
    defines unique Var p of type PathTy()
    
  PathPatternRef(p):
    refers to PathPattern p

  // Note 1: SelectVar is non-unique because we already do the uniqueness test using Var
  // Note 2: In PGQL 1.0, in the SELECT, you cannot refer to variables previously defined in the SELECT
  ExpAsVar(v, exp):
    defines non-unique SelectVar v
    defines unique Var v of type ty where exp has type ty
  
  ExpAsGroupVar(v, exp):
    defines non-unique GroupVar v
    defines unique Var v of type ty where exp has type ty
    
  VarRef(v):
    refers to Var v
  
  GroupRef(v):
    refers to GroupVar v
      
  SelectOrGroupRef(v):
    refers to GroupVar v
      otherwise refers to SelectVar v // e.g. in [[SELECT AVG(n.age) as avgAge WHERE n ORDER BY avgAge]], avgAge in ORDER BY is a GroupRef and can only refer to SelectVar 
    
  NodeVarRef(n):
    refers to Var n of type NodeTy()

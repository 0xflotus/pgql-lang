module names

imports

  signatures/-

namespaces

  CommonPathExpression // regular PATH pattern
  MatchVar             // var defined in WHERE
  GroupVar             // var defined in GROUP BY
  SelectVar            // var defined in SELECT

binding rules // scoping

  NormalizedQuery(_, _, _, _, _, _, _, _, _, _):
    scopes MatchVar, SelectVar, GroupVar, CommonPathExpression

binding rules // variable definition

  CommonPathExpression(p, _, _, _):
    defines unique CommonPathExpression p
    scopes MatchVar, SelectVar, GroupVar

  Vertex(n):
    defines non-unique MatchVar n of type VertexTy()

  Edge(_, e, _, _):
    defines unique MatchVar e of type EdgeTy()

  Path(_, _, _, _, p, _):
    defines unique MatchVar p of type PathTy()

binding rules // variable reference

  PathPatternRef(p):
    refers to CommonPathExpression p

  ExpAsVar(exp, v, _, SelectClause()):
    defines unique SelectVar v of type ty where exp has type ty // unlike SQL, we currently don't allow duplicate column names (i.e. 'unique')

  ExpAsVar(exp, v, _, GroupByClause()):
    defines unique GroupVar v of type ty where exp has type ty // unlike SQL, we currently don't allow duplicate column names (i.e. 'unique')

  VarRef(v, MatchClause()):
    refers to MatchVar v

  VarRef(v, GroupByClause()):
    refers to GroupVar v

  VarRef(v, SelectClause()):
    refers to SelectVar v

module trans-for-nabl

imports

  signatures/-
  normalized-signatures
  common

/*
 * normalizeForNaBL is called after normalize. It's for extra transformations to get the AST in a shape that NaBL likes.
 *   PGQL -> PGQL AST -> PGQL norm. AST -> PGQL AST for NaBL -> GraphQuery object ==> query planner
 */
rules

  trans-for-nabl = alltd(trans-for-nabl-grouping)

rules

  // if there is a GROUP BY or aggregation in SELECT then all variables in the SELECT need to be group keys or need to be inside aggregates
  trans-for-nabl-grouping:
    Query(pathPatterns, expAsVars, fromClause, graphPattern, groupBy, orderBy, limitOffsets) ->
        Query(pathPatterns, expAsVars', fromClause, graphPattern, groupBy', orderBy', limitOffsets)
    with
      if (!groupBy; ?Some(_)) + (!expAsVars; oncetd(is-aggregate))
      then
        expAsVars' := <to-VarRef-or-GroupRefs; to-ExpAsSelectVars> expAsVars
      ; orderBy' := <to-VarRef-or-SelectOrGroupRefs> orderBy
      ; groupBy' := <to-ExpAsGroupVars> groupBy
      else
        expAsVars' := expAsVars
      ; orderBy' := <to-VarOrSelectRefs> orderBy
      ; groupBy' := groupBy
      end

  to-VarRef-or-GroupRefs = alltd(is-aggregate <+ ?VarRef(<id>); !GroupRef(<id>))
  to-VarRef-or-SelectOrGroupRefs = alltd(is-aggregate <+ ?VarRef(<id>); !SelectOrGroupRef(<id>))

  to-ExpAsSelectVars = alltd(to-ExpAsSelectVar)
  to-ExpAsSelectVar = ?ExpAsVar(exp, v); !ExpAsSelectVar(exp, v)

  to-ExpAsGroupVars = alltd(to-ExpAsGroupVar)
  to-ExpAsGroupVar = ?ExpAsVar(exp, v); !ExpAsGroupVar(exp, v)
  to-ExpAsGroupVar = ?AnonymousExpAsVar(exp, v); !AnonymousExpAsGroupVar(exp, v)

  to-VarOrSelectRefs = alltd(?VarRef(<id>); !VarOrSelectRef(<id>))
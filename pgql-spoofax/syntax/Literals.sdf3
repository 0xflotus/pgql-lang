module Literals

imports

  Names
  Legacy

context-free syntax

  Literal   = Number
  Literal   = Boolean
  Literal   = String
  Literal   = BindVariable
  Literal   = Date
  Literal   = Time
  Literal   = Timestamp

  AltLit.Or              = <<AltLit>|<AltLit>> {left}
  AltLit                 = Literal
  AltLit.String          = IDENTIFIER
  AltPath.Or             = <<AltPath>|<AltPath>> {left}
  AltPath.PathPatternRef = IDENTIFIER

context-free syntax

  Number.Integer            = UNSIGNED-INT
  Number.Decimal            = UNSIGNED-DECIMAL
  String.String             = LEGACY10-STRING
  Label.String              = IDENTIFIER
  Boolean.True              = <true> {case-insensitive}
  Boolean.False             = <false> {case-insensitive}
  BindVariable.BindVariable = <?>
  Date.Date                 = <DATE <STRING>> {case-insensitive}
  Time.Time                 = <TIME <STRING>> {case-insensitive}
  Timestamp.Timestamp       = <TIMESTAMP <STRING>> {case-insensitive}
  Literal.Null              = <null> {case-insensitive} // not valid syntax; just here so that we can generate an appropriate error to use IS NULL or IS NOT NULL instead

lexical syntax

  UNSIGNED-INT         = [0-9]+
  UNSIGNED-DECIMAL     = [0-9]* "." [0-9]+

  STRING               = "'" (~[\'\n\\] | ESCAPE-CHAR)* "'"

lexical restrictions // greedy matching

  UNSIGNED-INT     -/- [0-9]
  UNSIGNED-DECIMAL -/- [0-9]

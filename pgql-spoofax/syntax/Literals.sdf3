module Literals

imports

  Names

context-free syntax

  Literal   = Number
  Literal   = Boolean
  Literal   = String
  Literal   = BindVariable
  Literal   = Date
  Literal   = Time
  Literal   = Timestamp

  Labels.Or = <<Labels>|<Labels>> {left}
  Labels    = IDENTIFIER
  Labels    = STRING

context-free syntax

  Number.Integer            = UNSIGNED-INT
  Number.Decimal            = UNSIGNED-DECIMAL
  String.String             = STRING
  Label.String              = IDENTIFIER
  Boolean.True              = <true> {case-insensitive}
  Boolean.False             = <false> {case-insensitive}
  Literal.Null              = <null> {case-insensitive}
  BindVariable.BindVariable = <?>
  Date.Date                 = <date <STRING>> {case-insensitive}
  Time.Time                 = <time <STRING>> {case-insensitive}
  Timestamp.Timestamp       = <timestamp <STRING>> {case-insensitive}

lexical syntax

  UNSIGNED-INT         = [0-9]+
  UNSIGNED-DECIMAL     = [0-9]* "." [0-9]+

  STRING               = "'" (~[\'\n\\] | ESCAPE-CHAR)* "'"

lexical restrictions // greedy matching

  UNSIGNED-INT     -/- [0-9]
  UNSIGNED-DECIMAL -/- [0-9]

module pgql-lang

imports

  Layout
  Names
  Literals
  GraphPattern
  SolutionModifiers
  Legacy
  Expressions

context-free start-symbols

  Start

context-free syntax

  Start = Query

  Query = Legacy10Query

  Query.Query =
    <
      <{CommonPathExpression "\n"}*>
      <SelectClause>
      <FromClause?>
      <MatchClause>
      <WhereClause?>
      <GroupByClause?>
      <HavingClause?>
      <OrderByClause?>
      <LimitOffsetClauses?>
    >

  CommonPathExpression.CommonPathExpression = <PATH <IDENTIFIER> <AsignSymbol> <PathPattern> <WhereClause?>> {case-insensitive}
  AsignSymbol.Pgql11AsignSymbol = <AS>  {case-insensitive}

  SelectClause.SelectClause = <SELECT <Distinct?> <Projection>> {case-insensitive}
  Projection.ExpAsVars = <<{ExpAsVar ", "}+>>
  Projection.Star = <*>

  FromClause.FromClause = <FROM <Identifier>> {case-insensitive}

  WhereClause.WhereClause = <WHERE <Exp>> {case-insensitive}

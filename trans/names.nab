module names

imports
  
  src-gen/signatures/-
  signatures
  
namespaces
  
  Var
  ReturnVar
  GroupVar
  Schema

properties
 
  data of Schema: t
  
binding rules

  TopNode(_, _):
    scopes Var, ReturnVar, GroupVar, Schema
  
  SubQueries(_):
    scopes Var, ReturnVar, GroupVar

  Node(n):
    defines non-unique Var n of type NodeTy()
  
  Edge(_, e, _, _):
    defines unique Var e of type EdgeTy()

  PathDecl(p, _, _, _):
    defines unique Var p of type PathTy()
  
  // In PGQL 0.9, in the SELECT, you cannot refer to variables previously defined in the select
  ExpAsVar(v, _):
  	defines unique ReturnVar v
  	
  ExpAsGroupVar(v, _):
    defines unique GroupVar v
    
  VarRef(v):
    refers to Var v
    
  VarRefInOrderby(v):
    refers to Var v otherwise refers to ReturnVar v
    
  GroupRef(v):
    refers to GroupVar v
    
  NodeVarRef(n):
    refers to Var n of type NodeTy()

  ExistsFunc(_):
    scopes Var, ReturnVar, GroupVar
 
  m@Schema(_, _):
    implicitly defines non-unique Schema "schema" of data m
    

module names

imports
  
  src-gen/signatures/-
  signatures
  
namespaces
  
  Var
  SelectVar
  GroupVar
  Schema

properties
 
  data of Schema: t
  
binding rules

  TopNode(_, _):
    scopes Var, SelectVar, GroupVar, Schema
  
  SubQueries(_):
    scopes Var, SelectVar, GroupVar

  Node(n):
    defines non-unique Var n of type NodeTy()
  
  Edge(_, e, _, _):
    defines unique Var e of type EdgeTy()

  PathDecl(p, _, _, _):
    defines unique Var p of type PathTy()
  
  // SelectVar is non-unique because we already do the uniqueness test using Var
  // In PGQL 0.9, in the SELECT, you cannot refer to variables previously defined in the SELECT
  ExpAsVar(v, _):
  	defines non-unique SelectVar v
  	defines unique Var v
  
  // GroupVar and SelectVar are non-unique because we already do the uniqueness test using Var
  ExpAsGroupVar(v, _):
    defines non-unique GroupVar v
    defines unique Var v
    
  VarRef(v):
    refers to Var v
    
  GroupRef(v):
    refers to GroupVar v
      otherwise refers to SelectVar v // e.g. in [[SELECT AVG(n.age) as avgAge WHERE n ORDER BY avgAge]], avgAge in ORDER BY is a GroupRef and can only refer to SelectVar 
    
  NodeVarRef(n):
    refers to Var n of type NodeTy()

  ExistsFunc(_):
    scopes Var, SelectVar, GroupVar
 
  m@Schema(_, _):
    implicitly defines non-unique Schema "schema" of data m
    

module check

imports

  src-gen/signatures/-
  signatures-norm/-
  sig-normalized
  sig-nabl
  common
  lib/runtime/nabl/-
  lib/runtime/task/-
  lib/runtime/types/-
  lib/runtime/properties/-
  names  

rules // cross-constraints involving paths
  
  nabl-constraint(|ctx):
    Constraints(constraints) -> <fail>
    where
      <map(try(constraints-check(|ctx)))> constraints
      
  constraints-check(|ctx):
    constraint -> <fail>
    where
      varRefs := <collect-om(?VarRef(_))> constraint;
      <gt> (<length> varRefs, 1);
      <alltd(cross-constraints-paths(|ctx))> constraint
  
  cross-constraints-paths(|ctx):
    PropRef(VarRef(v), _) -> <fail>
    where
      var-is-path := <var-is-path(|ctx)> v;
      <task-create-error-on-success(|ctx, var-is-path, $[Cross-constraints involving path variables not supported])> v

  var-is-edge(|ctx) = var-is-type(|ctx, "VarIsEdge")
  var-is-node(|ctx) = var-is-type(|ctx, "VarIsNode")
  var-is-path(|ctx) = var-is-type(|ctx, "VarIsPath")

  var-is-type(|ctx, type):
    v -> rewrite-task1
    where
      v-resolve-task := <nabl-create-collectdefs(|ctx)> v;
      v-prop-task    := <prop-create-lookup(|ctx, Type())> v-resolve-task;
      rewrite-task1  := <task-create-rewrite(|ctx, type)> v-prop-task

rules // subqueries
  
  nabl-constraint(|ctx):
    SubQueries(queries)  -> <fail>
    where
      <map(subquery-limitiation(|ctx))> queries
      
  subquery-limitiation(|ctx):
    e@Query(_, _, _, _) -> <id>
    where
      <generate-error(|ctx, $[Subqueries are not supported])> e

rules // (NOT) EXISTS limitations
  
  constraints-check(|ctx):
    e@ExistsFunc(_) -> <fail>
    where
      <generate-error(|ctx,$[(NOT) EXISTS is not supported])> e
  
rules // DISTINCT limitation
  
  nabl-constraint(|ctx):
    e@Distinct() -> <fail>
    with
      <generate-error(|ctx, $[DISTINCT is not supported])> e

rules // Unsupported functions
  
  nabl-constraint(|ctx):
    t -> <fail>
    where
      <?Src(_) + ?Dst(_)
       + ?Nodes(_) + ?Edges(_) + ?Length(_)
       + ?Head(_) + ?Last(_) + ?Init(_) + ?Tail(_)
       + ?Contains(_, _, _) + ?BeginsWith(_, _, _) + ?EndsWith(_, _, _)
       + ?Year(_) + ?Month(_) + ?Day(_) + ?Hours(_) + ?Minutes(_) + ?Seconds(_) + ?Timezone(_)
       + ?Case(_, _, _)
      > t;
      funcName := <get-constructor> t;
      <generate-error(|ctx, $[No such function: '[funcName]'])> t

rules // data types
  
  nabl-constraint(|ctx):
    LitCustomType(_, type) -> <fail>
    with
      <generate-error(|ctx, $[Custom data types are not supported])> type
  
rules // Unresolved/duplicate variables
  
  nabl-check-disable-unresolved = id
  nabl-check-disable-duplicate(|uri, ns) = id
    
  nabl-constraint(|ctx):
    VarRef(v) -> <fail>
    where
    	<generate-error-on-unresolved(|ctx, "Unresolved variable")> v
      
  nabl-constraint(|ctx):
    expAsVar -> <fail>
    where
      <?ExpAsVar(v, _) + ?ExpAsGroupVar(v, _)> expAsVar;
      <generate-error-on-duplicate(|ctx, NablNsVar(), "Duplicate variable definition")> v

  nabl-constraint(|ctx):
    Edge(_, e, _, _) -> <fail>
    where
      <generate-error-on-duplicate(|ctx, NablNsVar(), "Duplicate edge variable")> e

rules // in-lined constraints
  
  nabl-constraint(|ctx):
    ReferenceInInlinedConstraint(v) -> <fail>
    with
      <generate-error(|ctx, $['[v]' should be omitted here])> v
  
  nabl-constraint(|ctx):
    t@CrossReferenceInInlinedConstraint() -> <fail>
    with
      <generate-error(|ctx, "In-lined constraint may not access other variables")> t

rules

  // expressions in SELECT, ORDER BY need to be simple
  nabl-constraint(|ctx):
    t -> <fail>
    where
      <?ExpAsVars(_) + ?OrderClause(_)> t
    with
      exps := <not-supported-expressions> t;
      <batch-generate-error(|ctx, "Only simple expressions allowed here: variable, property access, aggregation or function call")> exps;
      expsInAggr := <not-supported-expressions-in-aggregration> t;
      <batch-generate-error(|ctx, "Only simple expressions allowed here: variable, property access or function call")> expsInAggr

 // expressions in GROUP BY need to be simple
  nabl-constraint(|ctx):
    t@GroupClause(_) -> <fail>
    with
      exps := <not-supported-expressions> t;
      <batch-generate-error(|ctx, "Only simple expressions allowed here: variable, property access or function call")> exps;
      aggregations := <collect-om(is-aggregate, conc)> t;
      <batch-generate-error(|ctx, "Aggregation not allowed in GROUP BY")> aggregations
  
  not-supported-expressions = collect-om(is-aggregate <+ is-function <+ is-expression <+ is-literal, conc); remove-all(is-aggregate + is-function)
  not-supported-expressions-in-aggregration = collect-om(is-aggregate, conc); collect-om(is-expression <+ is-literal, conc)
  
  nabl-constraint(|ctx):
    Query(_, _, _, Select(_, Vars(vars), groupClause, _, Some(orderClause), _)) -> <fail>
    where
      None() := groupClause;
      [] := <collect(is-aggregate)> vars // there are no aggregations in the SELECT
    with
      aggregations := <collect-om(is-aggregate, conc)> orderClause;
      <batch-generate-error(|ctx, "Aggregation in ORDER BY only allowed if SELECT constains aggregations, or, in combination with GROUP BY")> aggregations

  nabl-constraint(|ctx):
    Query(_, _, _, Select(_, Vars(select-exps), group-by, _, order-by, _)) -> <fail>
    with group-refs := <collect-om(?GroupRef(<id>), conc)> select-exps
    	 ; select-or-group-refs := <collect-om(?SelectOrGroupRef(<id>), conc)> order-by
    	 ; if None() := group-by
    	     then <batch-generate-error-on-unresolved(|ctx, "Aggregation expected here since SELECT has other aggregation")> group-refs // error in SELECT
    	        ; <batch-generate-error-on-unresolved(|ctx, "Aggregation expected here since SELECT has aggregation")> select-or-group-refs // error in ORDER BY
    	     else <batch-generate-error-on-unresolved(|ctx, "Non-group key variable in SELECT")> group-refs // error in SELECT
              ; <batch-generate-error-on-unresolved(|ctx, "Cannot order by non-group key variable: use aggregation")> select-or-group-refs // error in ORDER BY
    	   end

  nabl-constraint(|ctx):
    t -> <fail>
    where
      <is-aggregate> t
    with
      aggrsInAggr := <get-arguments; collect-om(is-aggregate, conc)> t;
      <batch-generate-error(|ctx, "Nested aggregation is not allowed")> aggrsInAggr  

rules // helper rules

  batch-generate-error(|ctx, m) = map(generate-error(|ctx, m))
    
  generate-error(|ctx, m):
    t -> <id>
    with
      failtask := <task-create-fail(|ctx)>;
      <task-create-error-on-failure(|ctx, failtask, m)> t

  batch-generate-error-on-unresolved(|ctx, m) = map(generate-error-on-unresolved(|ctx, m))

  generate-error-on-unresolved(|ctx, m):
  	v -> <id>
  	with
  	  resolve-task := <nabl-create-collectdefs(|ctx)> v;
      <task-create-error-on-failure(|ctx, resolve-task, m)> v

  generate-error-on-duplicate(|ctx, namespace, m):
    v -> <id>
    with
      uri := <nabl-uri; nabl-uri-parent> v;
      resolve-task := <nabl-create-resolvedefs(|ctx, NablNsVar(), v, [])> uri;
      <task-create-error-on-multiple(|ctx, resolve-task, m)> v
      
signature constructors
  
  ReferenceInInlinedConstraint : var -> ReferenceInInlinedConstraint
  CrossReferenceInInlinedConstraint : CrossReferenceInInlinedConstraint

rules // future PGQL versions

  nabl-constraint(|ctx):
    t@Regex(_, _) -> <fail>
    with
      <generate-error(|ctx, $[Regular expression matching is not supported])> t
      
/*      
  nabl-constraint(|ctx):
    PathDecl(v, _, _, _) -> <fail>
    with
      <generate-error(|ctx, $[Path variables are not supported])> v

  nabl-constraint(|ctx):
    t@PathLength(_, _) -> <fail>
    with
      <generate-error(|ctx, $[Path queries are not supported])> t
            
  nabl-constraint(|ctx):
    t@Label(_) -> <fail>
    with
      <generate-error(|ctx, $[Labels are not supported])> t
*/

rules // Oracle PGX specific (TODO: remove this from the project)

rules // Not a connected graph, e.g. WHERE x -> y, x2 -> y2
  
  nabl-constraint(|ctx):
    GraphPattern(Nodes(nodes), Edges(edges), _, _, _, _, _, _) -> <fail>
    where
      sorted-partitions := <strip-annos; partition(|edges); reverse; qsort(length-gt)> nodes;
      biggest-partition := <Hd> sorted-partitions;
      other-partitions := <Tl> sorted-partitions
    with
      <map(connected-pattern-constraint(|ctx, biggest-partition))> other-partitions

  connected-pattern-constraint(|ctx, biggest-partition):
    other-partition -> <id>
    with
      if 1 := <length> biggest-partition
      then
        m := $[Node [<Hd> other-partition] is not connected to node [<Hd> biggest-partition]]
      else
        if 1 := <length> other-partition
        then m := $[Node [<Hd> other-partition] is not connected to nodes [<print-nodes> biggest-partition]]
        else m := $[Nodes [<print-nodes> other-partition] are not connected to nodes [<print-nodes> biggest-partition]]
        end
      end;
      <generate-error(|ctx, m)> other-partition

  print-nodes = reverse; separate-by(|", "); concat-strings

  length-gt = ?(l1, l2); !(<length> l1, <length> (l2)); gt

  partition(|edges):
    nodes -> partitions
    with
      nodes' := <map(?Node(<id>))> nodes;
      if [] := nodes
      then
        partitions := []
      else
        someNode := <Hd; get-reachable-nodes(|edges)> nodes';
        partitions := <partition-helper(|nodes', edges)> [someNode]
      end
      
  partition-helper(|nodes, edges):
    partitions -> partitions'
    with
      covered := <concat> partitions;
      uncovered := <diff> (nodes, covered);
      if [] := uncovered
      then
        partitions' := partitions
      else
        partition := <Hd; get-reachable-nodes(|edges)> uncovered;
        partitions' := <partition-helper(|nodes, edges)> [partition|partitions]
      end

  get-reachable-nodes(|edges):
    x -> result
    with
      nodes := <is-list <+ ![<id>]> x;
      nodes' := <map(get-reachable-nodes-helper(|edges));  concat; make-set> nodes;
      if <eq> (<length> nodes, <length> nodes')
      then result := nodes'
      else result := <get-reachable-nodes(|edges)> nodes'
      end

  get-reachable-nodes-helper(|edges):
    node -> result
    with
      outNeighbors := <filter(?Edge(node, _, <id>, _))> edges;
      inNeighbors := <filter(?Edge(<id>, _, node, _))> edges;
      result := <conc> (outNeighbors, inNeighbors, [node])

